emulate -L zsh -o extendedglob -o warncreateglobal -o typesetsilent

[[ -z "${ZINIT_ICE[submods]}" ]] && return 0

[[ "$1" = plugin ]] && \
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="$5" || \
    local type="$1" url="$2" id_as="$3" dir="$4"

local -a mods parts
local mod

mods=( ${(@s.;.)ZINIT_ICE[submods]} )
for mod in "${mods[@]}"; do
    parts=( "${(@s:->:)mod}" )
    # Remove only leading and trailing whitespace
    parts=( "${parts[@]//((#s)[[:space:]]##|[[:space:]]##(#e))/}" )

    print -r -- "z-a-submods: ${ZINIT[col-bar]}@@${ZINIT[col-rst]} Cloning submodule ${ZINIT[col-bar]}@@${ZINIT[col-rst]}: ${ZINIT[col-pname]}${parts[1]}${ZINIT[col-rst]}" \
            "to ${ZINIT[col-info]}${parts[2]}${ZINIT[col-rst]}"
    if [[ "${parts[1]}" != (http|https|ftp|ftps|scp)://* ]]; then
        parts[1]="https://github.com/${parts[1]}"
    fi

    command git -C "$dir" clone --progress "${parts[1]}" "${parts[2]}" |& \
        ${ZINIT[BIN_DIR]}/git-process-output.zsh
done

# vim:ft=zsh:sts=4:sw=4:et
